import com.wiredforcode.gradle.spawn.*

plugins {
    id 'idea'
    id 'groovy'
    id 'io.ratpack.ratpack-groovy' version '1.3.3'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'com.wiredforcode.spawn' version '0.6.0'
}

allprojects {
    repositories {
        jcenter()
    }

    group = 'org.10ne.prez'
    version = '1-SNAPSHOT'
}

subprojects {
    if (it.name == 'client') {
        return
    }
    apply plugin: 'io.ratpack.ratpack-groovy'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.wiredforcode.spawn'

    task startServer(type: SpawnProcessTask, dependsOn: 'shadowJar') {
        command "java -jar ${projectDir}/build/libs/${project.name}-${project.version}-all.jar"
        ready 'Ratpack started'
    }

    task stopServer(type: KillProcessTask)
}

idea {
    project {
        jdkName "1.8"
        languageLevel "1.8"
        vcs 'Git'
    }
}
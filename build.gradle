import com.wiredforcode.gradle.spawn.*

plugins {
    id 'idea'
    id 'groovy'
    id 'io.ratpack.ratpack-groovy' version '1.3.3'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'com.wiredforcode.spawn' version '0.6.0'
}

allprojects {
    repositories {
        jcenter()
    }

    group = 'org.10ne.prez'
    version = '1-SNAPSHOT'

    apply plugin: 'idea'
}

subprojects {
    if (it.name == 'client') {
        return
    }
    apply plugin: 'io.ratpack.ratpack-groovy'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.wiredforcode.spawn'

    task startServer(type: SpawnProcessTask, dependsOn: 'shadowJar') {
        directory = projectDir

        command assembleStartServerCommand(project)
        ready 'Ratpack started'
    }

    task stopServer(type: KillProcessTask) {
        directory = projectDir
    }
}

project(':client') {
    apply plugin: 'groovy'

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.4.6'
    }

    task runScript(dependsOn: 'classes', type: JavaExec) {
        systemProperties(SECRETARY_NODE: 'http://localhost:5050')
        main = 'Client'
        classpath = sourceSets.main.runtimeClasspath
    }
}

task startSuite << {
    println 'Started service suite'
}

startSuite.dependsOn {
    getTasksByName('startServer', true)
}


task stopSuite << {
    println 'Stopped service suite'
}

stopSuite.dependsOn {
    getTasksByName('stopServer', true)
}

idea {
    project {
        jdkName "1.8"
        languageLevel "1.8"
        vcs 'Git'
    }
}

private String assembleStartServerCommand(projectToStart) {
    def serviceEnvFile = projectToStart.projectDir.toPath().resolve('env')
    def systemProperties = serviceEnvFile.readLines().collect { "-D$it" }.join(' ')

    def serviceJarPath = "${projectToStart.projectDir}/build/libs/${projectToStart.name}-${projectToStart.version}-all.jar"

    "java $systemProperties -jar $serviceJarPath"
}